buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.5.0'
	}
}

apply plugin: 'org.standardout.bnd-platform'

repositories {
	jcenter()
	maven {
		url 'https://dl.bintray.com/simon-scholz/RxJava-Extensions/'
	}
}

platform {

	// Set a proper path to your eclipse installation (necessary for building a p2 updatesite)
	eclipseHome = new File('/home/simon/eclipse/eclipse-photon')

	// configure proper names for the p2 update site
	categoryId = 'Open-Req-Eclipse-Libs'
	categoryName = 'Open Req Eclipse Libs'

	featureId = 'All-Open-Req-Eclipse-Libs'
	featureName = 'All Open Req Eclipse Libs'
	featureVersion = '0.1.0.'
	featureProvider = 'vogella GmbH'

	defaultQualifier = ''
	useBndHashQualifiers = false
	useFeatureHashQualifiers = false
	defaultQualifierMap.prefix = ''

	feature(id: 'Project-Reactor', name: 'Project Reactor', version: '3.1.4') {

		plugin('io.projectreactor:reactor-core:3.1.4.RELEASE') {
			bnd {
				optionalImport 'javax.swing'
				optionalImport 'kotlin'
				optionalImport 'kotlin.jvm.functions'
				optionalImport 'kotlin.jvm.internal'
				optionalImport 'sun.misc'
			}
		}

		plugin('io.projectreactor.addons:reactor-extra:3.1.5.RELEASE') {
			bnd {
				optionalImport 'javax.swing'
				optionalImport 'kotlin'
				optionalImport 'kotlin.jvm.functions'
				optionalImport 'kotlin.jvm.internal'
			}
		}
	}
	
	feature(id: 'Retrofit', name: 'Retrofit', version: '2.3.0') {
		// Convert the retrofit artifact to OSGi, make android.os optional and handle the split package problems in OSGi
		plugin('com.squareup.retrofit2:retrofit:2.3.0'){
			bnd {
				optionalImport 'android.os'
				optionalImport 'android.net'
		    	}
		}
	
		// Convert the retrofit jackson converter artifact to OSGi and handle the split package problems in OSGi
		plugin('com.squareup.retrofit2:converter-jackson:2.3.0')

		// lets retrofit return reactor types
		plugin('com.jakewharton.retrofit:retrofit2-reactor-adapter:2.1.0')
	
		// Convert okhttp3 artifact to OSGi without android
		plugin('com.squareup.okhttp3:okhttp:3.8.0') {
			bnd{
				optionalImport 'android.util'
			}
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.5.1'
}
